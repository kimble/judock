buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: 'java'

repositories {
    jcenter()
}


group = 'com.developerb.judock'
version = '0.5.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'com.spotify:docker-client:2.7.19',
            'org.glassfish.jersey.core:jersey-client:2.17',
            'joda-time:joda-time:2.7',
            'junit:junit:4.12'

    testCompile 'org.slf4j:slf4j-simple:1.7.12',
                'mysql:mysql-connector-java:5.1.35'
}



task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publications = [ 'mavenJava' ] // see publications closure

    pkg { //package will be created if does not exist
        repo = 'maven'
        name = 'judock'
        desc = 'Integration testing with JUnit and Docker'

        websiteUrl = 'https://github.com/kimble/judock'
        issueTrackerUrl = 'https://github.com/kimble/judock/issues'
        vcsUrl = 'https://github.com/kimble/judock.git'

        licenses = ['Apache-2.0']
        labels = ['java', 'junit', 'docker', 'testing']

        publicDownloadNumbers = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
